plugins {
    id 'java'
    id 'maven-publish'
}

group = 'co.killionrevival.sellfromshulker'
version = '1.0.1'

def javaVersion = JavaVersion.VERSION_21
def encoding = 'UTF-8'
def paperVersion = '1.21.4-R0.1-SNAPSHOT'
def mcVersion = '1.21.4'

repositories {
    gradlePluginPortal()
    mavenCentral()
    maven {
        name 'papermc-repo'
        url 'https://repo.papermc.io/repository/maven-public/'
    }
    maven { 
        url 'https://jitpack.io' 
    }
    maven {
        name 'nightexpress-releases'
        url 'https://repo.nightexpressdev.com/releases'
    }
    maven {
        url "https://maven.pkg.github.com/KillionRevival/KillionCommons"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paperVersion}"
    compileOnly "co.killionrevival:killioncommons:3.3.2-SNAPSHOT"

    implementation 'com.github.Gypopo:EconomyShopGUI-API:1.7.3'
    implementation ("su.nightexpress.coinsengine:CoinsEngine:2.4.2") {
        exclude group: 'org.spigotmc'
    }

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

tasks.withType(JavaCompile) {
    options.encoding = encoding
}

tasks.processResources {
    filesMatching(['plugin.yml', 'paper-plugin.yml']) {
        expand (version: project.version, mcVersion: mcVersion)
    }
}

